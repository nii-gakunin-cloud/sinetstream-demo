plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'net.sinetstream'
version = currentVersion

repositories {
    maven {
        url "https://gitlab.vcp-handson.org/api/v4/groups/2/-/packages/maven"
        if (project.hasProperty('gitLabPrivateToken')) {
            credentials(HttpHeaderCredentials) {
                name = 'Private-Token'
                value = gitLabPrivateToken
            }
        } else {
            credentials(HttpHeaderCredentials) {
                name = "Job-Token"
                value = project.hasProperty('gitLabJobToken') ? gitLabJobToken : System.getenv('CI_JOB_TOKEN')
            }
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }
    maven { url "https://packages.confluent.io/maven/" }
    mavenCentral()
}

dependencies {
    implementation group: 'org.apache.kafka', name: 'connect-runtime', version: '3.6.0'
    implementation group: 'org.apache.avro', name: 'avro', version: '1.11.3'
    implementation group: 'net.sinetstream', name: 'sinetstream-kafka-serde', version: '0.1.11'
    implementation group: 'io.confluent', name: 'kafka-connect-avro-data', version: '7.5.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        def env = System.getenv()
        maven {
            url "${env.CI_API_V4_URL}/projects/${env.CI_PROJECT_ID}/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = "Job-Token"
                value = env.CI_JOB_TOKEN
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}
